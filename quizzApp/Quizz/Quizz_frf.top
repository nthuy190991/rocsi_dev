topic: ~Quizz()
language: frf

################ include ################
include:lex_frf.top

################ concept ################
concept:(notUnderstood) ^rand["pardon? ^stayInScope " "excuse-moi je n'ai pas compris! ^stayInScope "] 

concept:(faux) 
^rand[
    "Désolé, ce n'est pas la bonne réponse"
    "Votre réponse est incorrecte"
    "Vous n'avez pas choisi la bonne réponse"
]

concept:(vrai) 
^rand[
    "C'est la bonne réponse" 
    "Votre culture sur ce sujet est époustouflante"
    "Félicitation vous êtes incollable sur ce sujet" 
]

concept:(choix) vous avez choisi

concept:(merci) Je vous remercie d'avoir participé à ce quizz. \pau=8000\ $QuizzApp/ShowLogo=1

concept:(erreurJson) "Toutes mes excuses, je n'ai pas pu charger le quizz que vous avez demandé"

#résultat entre 0 et 25%
concept:(x) "Aie aie, vous devriez réviser encore un peu" 
#résultat entre 25 et 50%
concept:(xx) "Vous ferez mieux la prochaine fois"
#résultat entre 50 et 75%
concept:(xxx) "Encore un effort et vous serez incolable sur ce sujet !" 
#résultat entre 75% et plus
concept:(xxxx) "Vous avez une bonne maitrise de ce sujet"
#résultat entre 100%
concept:(xxxxx) "Félicitation, vous avez obtenu le meilleur score ! Ne seriez vous pas de la famille d'Albert Einstein par hasard ?"

################ dynamic ################
dynamic:rep0
dynamic:rep1
dynamic:rep2

################ proposal ################
proposal: %start Bonjour je suis pepper, je suis prét à jouer avec vous ! 

proposal: %restart Voulez vous \pau=500\ relancer le quizz,\pau=500\ ne rien faire pour le moment ou stopper l'application ?
    u1:(relancer le quizz) Commençons le quiz \pau=1000\ $QuizzApp/QuizzId=$QuizzApp/CurrantQuizz
    #u1:(choisir un autre quizz) indisponible pour le moment
    u1:(ne rien faire pour le moment) Comme il vous plaira !
    u1:(stopper l'application) Je ferme l'application quizz $QuizzApp/Exit=1
    
proposal: %check
    u1:(e:QuizzApp/Vrai) ~vrai \pau=1000\ $QuizzApp/Next=1
    u1:(e:QuizzApp/Faux) ~faux \pau=1000\ $QuizzApp/Next=1

################  dialog ################
#u:(e:Dialog/NoOneSpeak20) 
#[
#    "Tu es encore là?" 
#    "Je me sens si seul dans ce monde" 
#    "Oups, désolé, c'est trop tard! ^gotoReactivate(end)"
#]
#u:(e:Dialog/NotUnderstood) ~notUnderstood
#u:(~repeat_it) je disais: ^sameProposal


u:(e:QuizzApp/ErreurJson) ~erreurJson

u:(e:QuizzApp/Ready) ^gotoReactivate(start)

#_* ne fonctionnement pas pour le moment car il nous faut une licence spécifique
#u:(lancer le quizz _*) Trés bien \pau=1000\ Commençons le quiz \pau=1000\ $QuizzApp/CurrantQuizz=$1 $QuizzApp/QuizzId=$1 
#en attendant
u:(lancer le quizz) Trés bien \pau=1000\ Commençons le quiz \pau=1000\ $QuizzApp/CurrantQuizz=1 $QuizzApp/QuizzId=1  

u:(e:QuizzApp/Question) $QuizzApp/Question
    
    u1:(e:Dialog/NoOneSpeak20) ~notUnderstood
    
    #le joueur répond sur la tablette
    u1:(e:QuizzApp/Vrai) ~vrai \pau=1000\ $QuizzApp/Next=1
    u1:(e:QuizzApp/Faux) ~faux \pau=1000\ $QuizzApp/Next=1
    
    #le joueur répond vocallement en utilisant les lettres a, b ou c
    u1:({réponse} a) ~choix la réponse a $QuizzApp/Reponse="a" ^gotoReactivate(check)       
    u1:({réponse} b) ~choix la réponse b $QuizzApp/Reponse="b" ^gotoReactivate(check)
    u1:({réponse} c) ~choix la réponse c $QuizzApp/Reponse="c" ^gotoReactivate(check)
    
    #le joueur répond vocallement en énoncent la réponse entiére
    u1:(~rep0) ~choix ~rep0 $QuizzApp/Reponse="a" ^gotoReactivate(check)
    u1:(~rep1) ~choix ~rep1 $QuizzApp/Reponse="b" ^gotoReactivate(check)
    u1:(~rep2) ~choix ~rep2 $QuizzApp/Reponse="c" ^gotoReactivate(check)
    
#score du joueur
u:(e:QuizzApp/x) ~x ~merci ^gotoReactivate(restart)
u:(e:QuizzApp/xx) ~xx ~merci ^gotoReactivate(restart)
u:(e:QuizzApp/xxx) ~xxx ~merci ^gotoReactivate(restart)
u:(e:QuizzApp/xxxx) ~xxxx ~merci ^gotoReactivate(restart)
u:(e:QuizzApp/xxxxx) ~xxxxx ~merci ^gotoReactivate(restart)
